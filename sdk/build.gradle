
plugins {
    id 'net.linguica.maven-settings' version '0.5'
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'maven'

group 'mil.nga.giat.mage'
archivesBaseName = 'mage-android-sdk'
version '5.3.0'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

task sourceJar(type: Jar) {
    from project.android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

publishing {
    publications {
        aar(MavenPublication) {
            artifact "${project.buildDir}/outputs/aar/${archivesBaseName}-release.aar", {
                artifactId archivesBaseName
            }
            artifact sourceJar
            // the publication doesn't know about our dependencies, so we have to manually add them to the pom
            // epic fail android
            pom.withXml {
                // for dependencies and exclusions
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { Dependency dep ->
                    if (dep instanceof SelfResolvingDependency) {
                        // apparently the files() dependency results in a SelfResolvingDependency - skip it
                        return;
                    }
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)

                    // for exclusions
                    if (dep.excludeRules.size() > 0) {
                        def exclusions = dependencyNode.appendNode('exclusions')
                        dep.excludeRules.each { ExcludeRule ex ->
                            def exclusion = exclusions.appendNode('exclusion')
                            exclusion.appendNode('groupId', ex.group)
                            exclusion.appendNode('artifactId', ex.module)
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'build'
            url project.properties['repositories.build.url'] ?: "${buildDir}/m2"
        }
        // assuming a maven settings.xml defining credentials for a server with id equal to repositories.shared.name property value
        if (project.hasProperty('repositories.shared.name')) {
            maven {
                name project.properties['repositories.shared.name']
                url "${project.properties['repositories.shared.url']}"
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:preference-v7:25.1.1'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-jackson:2.0.0-beta2'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    compile 'com.vividsolutions:jts:1.8'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile files('libs/sanselanandroid.jar')
}
